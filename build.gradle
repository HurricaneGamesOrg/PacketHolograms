plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'maven-publish'
}


defaultTasks 'build'


group 'org.hurricanegames'
version '1.2'


import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.Files


Path projectDirectoryPath = projectDir.toPath().toAbsolutePath()


sourceCompatibility = JavaVersion.VERSION_16

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
	}
	maven {
		url 'https://papermc.io/repo/repository/maven-public/'
	}
    maven {
    	url 'https://repo.dmulloy2.net/nexus/repository/public/'
    }
	maven {
		url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
	}
	maven {
		url 'https://jitpack.io'
	}
}

configurations {
	runtimeDependencies
	compileOnly {
		extendsFrom runtimeDependencies
	}
	testImplementation {
	    extendsFrom runtimeDependencies
	}
}

dependencies {
	runtimeDependencies group: 'io.papermc.paper', name: 'paper-api', version: '1.17-R0.1-SNAPSHOT'
	runtimeDependencies group: 'me.clip', name: 'placeholderapi', version: '2.10.4'
	runtimeDependencies group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.5.0'
	runtimeDependencies group: 'com.github.ProtocolSupport', name: 'ProtocolSupport', version: 'a82f2b4627'

	implementation group: 'com.github.HurricaneGamesOrg', name: 'HurricaneGamesOrgPluginLib', version: '997fc2eb6b'
}

compileJava {
	options.encoding = 'UTF-8'
	options.incremental = false
}


processResources {
	filesMatching('plugin.yml') {
		expand 'version': project.version
	}
}

shadowJar {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	archiveClassifier = ""

	from sourceSets.main.java.srcDirs
	from 'LICENSE'

	exclude 'META-INF/**'
	exclude 'module-info.class'

	String relocatePrefix = group + '.' + rootProject.name.toLowerCase() + '.zlibs.'

	relocate 'org.hurricanegames.commandlib', relocatePrefix + 'org.hurricanegames.commandlib'

	minimize()
}


task copyFinalJarToTarget(type: DefaultTask) {doLast{
	Path targetJarDirectory = projectDirectoryPath.resolve('target')
	Files.createDirectories(targetJarDirectory)
	Files.copy(
		shadowJar.archiveFile.get().getAsFile().toPath().toAbsolutePath(),
		targetJarDirectory.resolve(shadowJar.archiveBaseName.get() + '.jar'),
		java.nio.file.StandardCopyOption.REPLACE_EXISTING
	)
}}


compileJava.dependsOn(clean)
jar.enabled = false
jar.finalizedBy(shadowJar)
shadowJar.finalizedBy(copyFinalJarToTarget)


tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

publishing {
	publications {
		release(MavenPublication) {
			from components.java

			artifacts = [shadowJar]
		}
	}
}
